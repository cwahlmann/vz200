5 CLEAR5000
10 GOSUB 2500:REM TITEL
20 REM --- NEUES SPIEL ---
30 ST=12:R$=""
40 GOSUB2400
60 GOSUB1000
70 PRINT@204,L$
72 IF W$=L$ THEN 200:REM GESCHAFFT
75 IF ST=0 THEN 300:REM VERLOREN
77 PRINT@256,R$
80 PRINT@320,"WELCHEN BUCHSTABEN RAETST DU";
90 INPUT B$:IF B$<"A" OR B$>"Z" THEN 80
95 GOSUB 2700:REM BUCHSTABEN MERKEN
100 GOSUB2600:REM CHECK
110 IF OK=0 THEN ST=ST-1
120 GOTO 60
200 REM --- GESCHAFFT
210 PRINT:PRINT"DU HAST DAS WORT ERRATEN!":PRINT
220 GOTO400
300 REM --- VERLOREN
310 PRINT:PRINT"+++ DAS WARS. DU HAENGST +++"
320 PRINT"DAS WORT WAR '";W$;"'":PRINT
330 GOTO400
400 REM --- ENDE
410 PRINT"NOCH EIN SPIEL (J/N)?"
420 A$=INKEY$
430 IF A$="J" THEN 20
440 IF A$<>"N" THEN 420
450 CLS:PRINT"BIS BALD!"
460 END
1000 REM HANG THE MAN
1010 REM ST=[0..10]
1015 CLS
1020 IF ST=0 THEN 1100
1021 IF ST=1 THEN 1200
1022 IF ST=2 THEN 1300
1023 IF ST=3 THEN 1400
1024 IF ST=4 THEN 1500
1025 IF ST=5 THEN 1600
1026 IF ST=6 THEN 1700
1027 IF ST=7 THEN 1800
1028 IF ST=8 THEN 1900
1029 IF ST=9 THEN 2000
1030 IF ST=10 THEN 2100
1031 IF ST=11 THEN 2200
1032 IF ST=12 THEN 2300
1033 RETURN
1100 PRINT"   ==== "
1110 PRINT"   I/ : "
1120 PRINT"   I  O "
1130 PRINT"   I /#\"
1140 PRINT"   I  !\"
1150 PRINT"  JIL   "
1160 PRINT"-=====---"
1170 RETURN
1200 PRINT"   ==== "
1210 PRINT"   I/ : "
1220 PRINT"   I  O "
1230 PRINT"   I /# "
1240 PRINT"   I  !\"
1250 PRINT"  JIL   "
1260 PRINT"-=====---"
1270 RETURN
1300 PRINT"   ==== "
1310 PRINT"   I/ : "
1320 PRINT"   I  O "
1330 PRINT"   I  # "
1340 PRINT"   I  !\"
1350 PRINT"  JIL   "
1360 PRINT"-=====---"
1370 RETURN
1400 PRINT"   ==== "
1410 PRINT"   I/ : "
1420 PRINT"   I  O "
1430 PRINT"   I  # "
1440 PRINT"   I   \"
1450 PRINT"  JIL   "
1460 PRINT"-=====---"
1470 RETURN
1500 PRINT"   ==== "
1510 PRINT"   I/ : "
1520 PRINT"   I  O "
1530 PRINT"   I  # "
1540 PRINT"   I    "
1550 PRINT"  JIL   "
1560 PRINT"-=====---"
1570 RETURN
1600 PRINT"   ==== "
1610 PRINT"   I/ : "
1620 PRINT"   I  O "
1630 PRINT"   I    "
1640 PRINT"   I    "
1650 PRINT"  JIL   "
1660 PRINT"-=====---"
1670 RETURN
1700 PRINT"   ==== "
1710 PRINT"   I/ : "
1720 PRINT"   I    "
1730 PRINT"   I    "
1740 PRINT"   I    "
1750 PRINT"  JIL   "
1760 PRINT"-=====---"
1770 RETURN
1800 PRINT"   ==== "
1810 PRINT"   I/   "
1820 PRINT"   I    "
1830 PRINT"   I    "
1840 PRINT"   I    "
1850 PRINT"  JIL   "
1860 PRINT"-=====---"
1870 RETURN
1900 PRINT"   ==== "
1910 PRINT"   I    "
1920 PRINT"   I    "
1930 PRINT"   I    "
1940 PRINT"   I    "
1950 PRINT"  JIL   "
1960 PRINT"-=====---"
1970 RETURN
2000 PRINT"        "
2010 PRINT"   I    "
2020 PRINT"   I    "
2030 PRINT"   I    "
2040 PRINT"   I    "
2050 PRINT"  JIL   "
2060 PRINT"-=====---"
2070 RETURN
2100 PRINT"        "
2110 PRINT"   I    "
2120 PRINT"   I    "
2130 PRINT"   I    "
2140 PRINT"   I    "
2150 PRINT"  JI    "
2160 PRINT"-=====---"
2170 RETURN
2200 PRINT"        "
2210 PRINT"        "
2220 PRINT"        "
2230 PRINT"        "
2240 PRINT"        "
2250 PRINT"  J     "
2260 PRINT"-=====---"
2270 RETURN
2300 PRINT"        "
2310 PRINT"        "
2320 PRINT"        "
2330 PRINT"        "
2340 PRINT"        "
2350 PRINT"        "
2360 PRINT"-=====---"
2370 RETURN
2400 REM --- WORT AUSSUCHEN ---
2401 CLS:PRINT"WILLST DU..."
2402 PRINT"1: EIN EIGENES WORT EINGEBEN"
2403 PRINT"2: ZUFAELLIG EINS RATEN"
2404 INPUT A$: IF A$<"1" OR A$>"2" THEN 2404
2405 IF A$="2" THEN 2410
2406 PRINT "WIE HEISST DEIN WORT":INPUT W$
2407 GOTO2450
2410 REM --- WORT AUSSUCHEN ---
2415 RESTORE
2420 READ C
2430 N=RND(C)
2440 FOR I=1 TO N:READ W$:NEXT
2450 L$=""
2460 FOR I=1 TO LEN(W$)
2470 B$=MID$(W$,I,1)
2480 IF B$>="A" AND B$<="Z" THEN 2482
2481 L$=L$+B$:GOTO 2490
2482 L$=L$+"-"
2490 NEXT:RETURN
2500 REM --- TITEL ---
2505 CLS
2510 PRINT:PRINT:PRINT:PRINT
2520 PRINT"          GALGENMANN"
2530 PRINT
2540 PRINT"       (C) 2019 BY FR3D"
2550 PRINT@480,"  DRUECKE <SPACE> ZUM STARTEN";
2560 IF INKEY$<>" " THEN 2560
2570 RETURN
2600 REM --- CHECK ---
2610 OK=0
2620 L0$=""
2630 FOR I=1 TO LEN(W$)
2640 IF MID$(L$,I,1) <> "-" THEN 2670
2650 IF MID$(W$,I,1) <> B$ THEN 2670
2660 L0$=L0$+MID$(W$,I,1):OK=1:GOTO2680
2670 L0$=L0$+MID$(L$,I,1)
2680 NEXT
2690 L$=L0$:RETURN
2700 REM --- BUCHSTABEN MERKEN
2710 FOR I=1 TO LEN(R$)
2720 IF B$=MID$(R$,I,1) THEN RETURN
2730 NEXT I
2740 R$=R$+B$
2750 RETURN
3000 REM WOERTER
3001 DATA26
3010 DATA "ABACUS"
3020 DATA "BIRNBAUM"
3030 DATA "CHEFSESSEL"
3040 DATA "DUMPFBACKE"
3050 DATA "ESELSMILCH"
3060 DATA "FERNVERKEHR"
3070 DATA "GEIGERZAEHLER"
3080 DATA "HEINZELMAENNCHEN"
3090 DATA "IDENTITAETSKRISE"
3100 DATA "JEDIRITTER"
3110 DATA "KRUEMELMONSTER"
3120 DATA "LUMMERLAND"
3130 DATA "MILCHBUBI"
3140 DATA "NIEMANDSLAND"
3150 DATA "ORTHOGRAPHIE"
3160 DATA "PURZELBAUM"
3170 DATA "QUALLENPLAGE"
3180 DATA "RUESSELSCHWEIN"
3190 DATA "SAMMELALBUM"
3200 DATA "TINTENKLEKS"
3210 DATA "UNFALLVERHUETUNG"
3220 DATA "VORSCHLAGHAMMER"
3230 DATA "WUNSCHPUNSCH"
3240 DATA "XAVIAR"
3250 DATA "YOGHURT"
3260 DATA "ZINKSALBE"